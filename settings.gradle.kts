import java.util.Properties

pluginManagement {
    repositories {
        google {
            content {
                includeGroupByRegex("com\\.android.*")
                includeGroupByRegex("com\\.google.*")
                includeGroupByRegex("androidx.*")
            }
        }
        mavenCentral()
        gradlePluginPortal()
    }
}

dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        google()
        mavenCentral()
        maven {
            val ghUsername = System.getenv("USERNAME") ?: getLocalProperty("USERNAME")
            val ghPassword = System.getenv("TOKEN") ?: getLocalProperty("TOKEN")
            url = uri("https://maven.pkg.github.com/${ghUsername}/REPOSITORY")
            credentials {
                username = ghUsername
                password = ghPassword
            }
        }
    }
}

fun getLocalProperty(propertyName: String): String {
    val localProperties = Properties().apply {
        val localPropertiesFile = File(rootDir, "local.properties")
        if (localPropertiesFile.exists()) {
            load(localPropertiesFile.inputStream())
        }
    }

    return localProperties.getProperty(propertyName) ?: run {
        throw NoSuchFieldException("Not defined property: $propertyName")
    }
}

rootProject.name = "Root"
include(":app")
include(":app_compose")
include(":domain")
include(":data")
include(":example")
